Program.Sub.ScreenSU.Start
Gui.F_Main..create
Gui.F_Main..caption("ADP Entry")
Gui.F_Main..size(2100,2790)
Gui.F_Main..position(0,0)
Gui.F_Main..event(UnLoad,unload)
Gui.F_Main..alwaysontop(True)
Gui.F_Main..fontname("arial")
Gui.F_Main..fontsize(8)
Gui.F_Main..forecolor(0)
Gui.F_Main..fontstyle(False,False,False,False,False)
Gui.F_Main..BackColor(-2147483633)
Gui.F_Main..controlbox(True)
Gui.F_Main..maxbutton(False)
Gui.F_Main..minbutton(True)
Gui.F_Main..mousepointer(0)
Gui.F_Main..moveable(True)
Gui.F_Main..sizeable(False)
Gui.F_Main..ShowInTaskBar(True)
Gui.F_Main..titlebar(True)
Gui.F_Main..MinX(0)
Gui.F_Main..MinY(0)
Gui.F_Main.txt1.create(textbox,"",True,1215,315,0,200,400,True,0,"arial",8,-2147483643,1)
Gui.F_Main.txt1.text("")
Gui.F_Main.txt1.maxLength(3)
Gui.F_Main.lbl1.create(label,"ADP Company",True,1320,255,1,195,195,True,0,"arial",8,-2147483633,0)
Gui.F_Main.txt2.create(textbox,"",True,1215,315,0,200,1200,True,0,"arial",8,-2147483643,1)
Gui.F_Main.txt2.text("")
Gui.F_Main.txt2.maxLength(8)
Gui.F_Main.lbl2.create(label,"Batch Number",True,1320,255,1,195,1005,True,0,"arial",8,-2147483633,0)
Gui.F_Main.cmdExport.create(button)
Gui.F_Main.cmdExport.caption("Export")
Gui.F_Main.cmdExport.visible(True)
Gui.F_Main.cmdExport.size(975,375)
Gui.F_Main.cmdExport.zorder(0)
Gui.F_Main.cmdExport.position(300,1700)
Gui.F_Main.cmdExport.enabled(True)
Gui.F_Main.cmdExport.fontname("arial")
Gui.F_Main.cmdExport.fontsize(8)
Gui.F_Main.cmdExport.event(Click,Process)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Variable.Global.DSN.Declare(String)
Program.Sub.Preflight.End

Program.Sub.Main.Start
Gui.F_main..Show

Program.Sub.Main.End

Program.Sub.Process.Start
F.Intrinsic.Control.SetErrorHandler("Process_Err")
F.Intrinsic.Control.ClearErrors

V.Local.sError.Declare(String)


Variable.local.export.declare(String)
Variable.local.export.redim(0,6)
Variable.local.hold.declare(String)
Variable.local.query.declare(String)
Variable.Local.I.declare(Long)
Variable.local.DIR.declare(String)
Variable.local.date.declare(String)
Variable.local.file.declare(Long)
Variable.local.filename.declare(String)
Variable.local.sResult.declare(String)
Variable.Local.time.Declare(String)
Variable.Local.id.Declare(String)
V.Local.shold.Declare(String)
v.Local.sname.Declare(String)
v.Local.sfirst.Declare(String)
v.Local.slast.Declare(String)
v.Local.ssql.Declare(String)

V.Local.shold.Set(V.Screen.F_Main!txt1.Text)
F.Intrinsic.Control.If(V.Local.shold.trim,=,"")
	F.Intrinsic.UI.Msgbox("Must enter an ADP Company Code")
	F.Intrinsic.Control.ExitSub
F.Intrinsic.Control.endif
V.Local.shold.Set(V.Screen.F_Main!txt2.text)
F.Intrinsic.Control.If(V.Local.shold.trim,=,"")
	F.Intrinsic.UI.Msgbox("Must enter a Batch Number")
	F.Intrinsic.Control.ExitSub
F.Intrinsic.Control.endif
Function.Odbc.Connection!Conx.Openconnection(Variable.Ambient.pDSN,Variable.Ambient.PUser,Variable.Ambient.PPass)

Function.Intrinsic.String.Left(Variable.Passed.888888,8,Variable.Local.date)
Function.Intrinsic.String.Mid(Variable.Passed.888888,9,6,Variable.Local.time)
Function.Intrinsic.String.Mid(Variable.Passed.888888,15,2,Variable.Local.id)
Function.Intrinsic.string.Left(Variable.Screen.F_Main!txt1.Text,3,Variable.Local.hold)
Function.Intrinsic.Control.If(Variable.Local.hold.Length,<,3)
	Function.intrinsic.string.concat(Variable.Local.hold,"_",Variable.Local.hold)
Function.Intrinsic.Control.EndIf
Function.Intrinsic.File.Gethandle(Variable.Local.File)
Function.Intrinsic.String.Concat(Variable.Caller.FilesDir,"\ADP","\EPI",Variable.Local.hold,Variable.Local.id,".csv",Variable.Local.Filename)
Function.Intrinsic.File.Openforwrite(Variable.Local.Filename,Variable.Local.File)

'Function.Intrinsic.File.Writeline(Variable.Local.File,"Co Code,Batch ID,File #, Reg Hours, O/T Hours,Hours 3 Code, Hours 3 Amount")
Function.Intrinsic.File.Writeline(Variable.Local.File,"Co Code,Batch ID,File #, Last Name, First Name, Dept, Rate, Reg Hours, O/T Hours, Other Hours")
f.Intrinsic.String.Build("SELECT EMPLOYEE, NAME, TYPE, HOURS, DEPT, SHIFT, EC FROM PYRL_LABOR_XFER WHERE RUN_CYMD = '{0}' and RUN_TIME = '{1}'",Variable.Local.date,Variable.Local.time,Variable.Local.query)
'Function.intrinsic.string.concat("SELECT EMPLOYEE, NAME, TYPE, HOURS,EC FROM PYRL_LABOR_XFER WHERE RUN_CYMD = '",Variable.Local.date,"' AND RUN_TIME = '",Variable.Local.time,"'",Variable.Local.query)
Function.Odbc.Connection!Conx.Openrecordsetro("rstX",Variable.Local.query)
Function.Intrinsic.Control.dountil(Variable.Odbc.Conx!Rstx.Eof,=,True)
	Variable.local.export.redim(0,10)
	Function.Intrinsic.string.Left(Variable.Screen.F_Main!txt1.Text,3,Variable.Local.hold)
	Gui.F_Main.txt1.Text(Variable.Local.hold)
	Variable.local.export(0).set(Variable.Local.hold)
	Function.Intrinsic.string.Left(Variable.Screen.F_Main!txt2.Text,8,Variable.Local.hold)
	Gui.F_Main.txt2.Text(Variable.Local.hold)
	Variable.local.export(1).set(Variable.Local.hold)
	Variable.local.export(2).set(Variable.Odbc.Conx!Rstx.Fieldval!EMPLOYEE)

	'get the employees first name and last name
	'split the name based on comma and set first and last name
	f.Intrinsic.String.Split(v.ODBC.Conx!rstX.FieldValLTrim!NAME," ",v.Local.sname)
	v.Local.export(3).Set(v.Local.sname(1))
	v.Local.export(4).Set(v.Local.sname(0))
	'5 Dept
	v.Local.export(5).Set(v.ODBC.Conx!rstX.FieldValTrim!DEPT)
	'6 Rate
	f.Intrinsic.String.Build("Select RATE from V_EMPLOYEE_MSTR where EMPLOYEE = '{0}' and SHIFT = '{1}'",v.ODBC.Conx!rstX.FieldValTrim!EMPLOYEE,v.ODBC.Conx!rstX.FieldValTrim!SHIFT,v.Local.ssql)
	Function.Odbc.Connection!Conx.Openrecordsetro("rstRate",Variable.Local.ssql)
	'set the RATE if found otherwise blank
	f.Intrinsic.Control.If(v.ODBC.Conx!rstRate.EOF,=,False)
		v.Local.export(6).Set(v.ODBC.Conx!rstRate.FieldValTrim!RATE)
	f.Intrinsic.Control.Else
		v.Local.export(6).Set("")
	f.Intrinsic.Control.EndIf
	f.ODBC.Conx!rstRate.Close
	'9 Other
	Function.Intrinsic.Control.If(Variable.Odbc.Conx!Rstx.Fieldval!TYPE,=,"EC")
		Variable.local.export(9).set(Variable.Odbc.Conx!Rstx.Fieldval!Hours)
	Function.Intrinsic.Control.elseif(Variable.Odbc.Conx!Rstx.Fieldval!TYPE,=,"HT")
		Variable.local.export(9).set(Variable.Odbc.Conx!Rstx.Fieldval!Hours)
	Function.Intrinsic.Control.ElseIf(Variable.Odbc.Conx!Rstx.Fieldval!TYPE,=,"DT")
		Variable.local.export(9).set(Variable.Odbc.Conx!Rstx.Fieldval!Hours)
	Function.Intrinsic.Control.EndIf
	'7 Reg Hours
	'8 OT Hours
	Function.Intrinsic.Control.If(Variable.Odbc.Conx!Rstx.Fieldval!TYPE,=,"RT")
		Variable.local.export(7).set(Variable.Odbc.Conx!Rstx.Fieldval!Hours)
	Function.Intrinsic.Control.EndIf
	Function.Intrinsic.Control.If(Variable.Odbc.Conx!Rstx.Fieldval!TYPE,=,"OT")
		Variable.local.export(8).set(Variable.Odbc.Conx!Rstx.Fieldval!HOURS)
	Function.Intrinsic.Control.EndIf

	Variable.local.hold.set("")
	Function.Intrinsic.Control.For(Variable.Local.I,Variable.Local.export.Lbound,Variable.Local.export.Ubound,1)
		Function.intrinsic.control.if(Variable.Local.I,=,0)
			Variable.Local.hold.Set(Variable.Local.export(0))
		Function.intrinsic.control.elseif(Variable.Local.i,<>,0)
			Function.intrinsic.string.concat(Variable.Local.hold,",",Variable.Local.export(Variable.Local.I),Variable.Local.hold)
		Function.intrinsic.control.endif
	Function.intrinsic.control.next(Variable.Local.I)
	Function.Intrinsic.File.Writeline(Variable.Local.File,Variable.Local.Hold)
	Function.Odbc.Conx!Rstx.movenext
Function.intrinsic.control.loop
Function.Odbc.Conx!Rstx.Close

Function.Intrinsic.File.Closefile(Variable.Local.File)

Function.Odbc.Connection!Conx.Close
Function.Intrinsic.Control.End

F.Intrinsic.Control.ExitSub

F.Intrinsic.Control.Label("Process_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: GCG_4406_ADP_Export.g2u",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub(unload)
Function.Intrinsic.Control.EndIf


Program.Sub.Process.End

Program.Sub.unload.Start
Function.Intrinsic.Control.End

Program.Sub.unload.End

Program.Sub.Comments.Start
${$0$}$$}$WAW$}$3/8/2017 8:57:34 AM$}$False
Program.Sub.Comments.End